buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE'
        classpath 'gradle.plugin.com.boxfuse.client:flyway-release:4.0.3'
        classpath group: 'org.postgresql', name: 'postgresql', version: '9.4.1208.jre7'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.flywaydb.flyway'

apply from: 'configuration.gradle'

idea {
    module {
        sourceDirs += file('generated/')
    }
}

jar {
    baseName = 'adenlie-adventure-service'
    version = '0.1.0'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
    generatedSourcesDir = file("${buildDir}/generated-sources")
    querydslVersion = "3.6.3"
}

configurations {
    querydslapt
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")
    compile("org.springframework.boot:spring-boot-devtools")
    compile group: 'postgresql', name: 'postgresql', version: '9.0-801.jdbc4'
    compile "org.flywaydb:flyway-core:4.0.3"
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
    compile "joda-time:joda-time:2.2"
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.3.Final'

    testCompile(group: 'junit', name: 'junit', version: '4.11')
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile(group: 'org.mockito', name: 'mockito-all', version: '1.9.0')

    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile "com.mysema.querydsl:querydsl-core:$querydslVersion"
    compile "com.mysema.querydsl:querydsl-jpa:$querydslVersion"
    querydslapt "com.mysema.querydsl:querydsl-apt:$querydslVersion"
}

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir generatedSourcesDir
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

flyway {
    url = "jdbc:postgresql://localhost:5432/${rootProject.config.adventure_db_url}"
    locations = ["filesystem:$project.projectDir/src/main/resources/db"]
    user = 'postgres'
    password = 'adminadventure'
    driver = 'org.postgresql.Driver'
    schemas = ['public']
    table = 'flyway_schema_version'
    baselineOnMigrate = true
    validateOnMigrate = false
    outOfOrder = true
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = generatedSourcesDir
}

compileJava {
    doFirst {
        generatedSourcesDir.mkdirs();
    }
    options.compilerArgs += ['-s', generatedSourcesDir]

    dependsOn generateQueryDSL
}
